import org.ajoberstar.grgit.Grgit

buildscript {
  apply from: "dependencies/versions.gradle"

  repositories {
    maven { url 'http://192.168.30.16:8081/nexus/content/groups/public' }
    google()
  }
  dependencies {
    classpath deps.android_gradle_plugin
    classpath deps.grgit
  }
}

allprojects {
  def git = Grgit.open(currentDir: projectDir)
  addGitVersion(git.describe(), git.tag.list().size())
  repositories {
    maven { url 'http://192.168.30.16:8081/nexus/content/groups/public' }
    google()
  }
}

subprojects {
  def isModule = false
  def isAppModule = isModule || it.name.equals("app")
  def dataBindingModules = ["app"]
  //使用dataBinding的模块
  def enableDataBinding = dataBindingModules.contains(it.name)

  apply plugin: isAppModule ? 'com.android.application' : 'com.android.library'

  android {
    compileSdkVersion build_versions.compile_sdk
    buildToolsVersion build_versions.build_tools
    defaultConfig {
      if (isAppModule) applicationId "com.mrtan.androidarc" //这里改成项目id
      minSdkVersion build_versions.min_sdk
      targetSdkVersion build_versions.target_sdk

      versionCode build_versions.androidVersionCode
      versionName build_versions.androidVersionName
      testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
      multiDexEnabled true
      vectorDrawables.useSupportLibrary = true //drawable支持
    }
    buildTypes {
      debug {
        proguardFiles getDefaultProguardFile('proguard-android.txt'),
            '../dependecies/proguard-rules-debug.pro'
        minifyEnabled false
        zipAlignEnabled true
        jniDebuggable true
      }
    }

    compileOptions {
      sourceCompatibility JavaVersion.VERSION_1_8
      targetCompatibility JavaVersion.VERSION_1_8
    }

    if (enableDataBinding) {
      dataBinding {
        enabled true
      }
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}
